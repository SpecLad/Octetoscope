/*
  This file is part of Octetoscope.
  Copyright (C) 2013 Octetoscope contributors (see /AUTHORS.txt)

  This program is free software: you can redistribute it and/or modify
  it under the terms of the GNU General Public License as published by
  the Free Software Foundation, either version 3 of the License, or
  (at your option) any later version.

  This program is distributed in the hope that it will be useful,
  but WITHOUT ANY WARRANTY; without even the implied warranty of
  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
  GNU General Public License for more details.

  You should have received a copy of the GNU General Public License
  along with this program.  If not, see <http://www.gnu.org/licenses/>.
*/

ext {
  scalaVersion = '2.10.0'
  scalaMajorVersion = scalaVersion.find(/\d+\.\d+/)
  scalaCompilerDep = "org.scala-lang:scala-compiler:${scalaVersion}"
  scalaLibraryDep = "org.scala-lang:scala-library:${scalaVersion}"
  scalaSwingDep = "org.scala-lang:scala-swing:${scalaVersion}"

  scalaTestVersion = '1.9.1'
  scalaTestDep = "org.scalatest:scalatest_${scalaMajorVersion}:${scalaTestVersion}"
}

subprojects {
  apply plugin: 'scala'

  repositories {
    mavenCentral()
  }

  dependencies {
    scalaTools scalaCompilerDep

    compile scalaLibraryDep

    testCompile scalaTestDep
  }

  targetCompatibility = '1.6'

  // The Ant task doesn't support setting compatibility levels,
  // and 1.5 compatibility is deprecated in Scala 2.10.
  compileScala.scalaCompileOptions.useAnt = false
  compileTestScala.scalaCompileOptions.useAnt = false

  test << {
    ant.taskdef(name: 'scalatest', classname: 'org.scalatest.tools.ScalaTestAntTask', classpath: classpath.asPath)
    ant.scalatest(runpath: testClassesDir, haltonfailure: 'true', fork: 'false') {
      reporter(type: 'stderr')
    }
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '1.5'
}
