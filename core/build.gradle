/*
  This file is part of Octetoscope.
  Copyright (C) 2013-2014 Octetoscope contributors (see /AUTHORS.txt)

  This program is free software: you can redistribute it and/or modify
  it under the terms of the GNU General Public License as published by
  the Free Software Foundation, either version 3 of the License, or
  (at your option) any later version.

  This program is distributed in the hope that it will be useful,
  but WITHOUT ANY WARRANTY; without even the implied warranty of
  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
  GNU General Public License for more details.

  You should have received a copy of the GNU General Public License
  along with this program.  If not, see <http://www.gnu.org/licenses/>.
*/

def generatedResourcesDir = new File(buildDir, 'generated/resources')

sourceSets {
  main {
    resources {
      srcDir generatedResourcesDir
    }
  }
}

task generateVersionInfo {
  def propFile = new File(generatedResourcesDir, 'ru/corrigendum/octetoscope/core/version.properties')
  outputs.file propFile

  doLast {
    // Not using the Properties class here to avoid the timestamp.
    def propString = """\
    releaseVersion = $versionInfo.releaseVersion
    extraCommits = $versionInfo.extraCommits
    commitHash = $versionInfo.commitHash
    dirty = $versionInfo.dirty
    """.stripIndent()

    propFile.parentFile.mkdirs()
    propFile.write(propString)
  }
}

processResources {
  dependsOn generateVersionInfo
}

dependencies {
  compile deps.arm

  compile project(':abstractinfra')
}
